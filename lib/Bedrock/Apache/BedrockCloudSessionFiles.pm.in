package Apache::BedrockCloudSessionFiles;

#    Copyright (C) 2025, TBC Development Group, LLC
#
#    This program is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    any later version.
#
#    This program is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with this program; if not, write to the Free Software
#    Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA 02111-1307 USA
#

use strict;
use warnings;

use Apache::Bedrock qw(cache);
use Bedrock qw(choose :booleans);
use Bedrock::Constants qw(:booleans);
use Bedrock::Apache::Constants qw($OK);
use Data::Dumper;
use English qw(-no_match_var);
use File::Type;

use Role::Tiny::With;
with 'Bedrock::Apache::BedrockS3Handler';
with 'Bedrock::Apache::HandlerUtils';

our $VERSION = '@PACKAGE_VERSION@';

########################################################################
sub handler {
########################################################################
  my ($r) = @_;

  my $bedrock_handler = Bedrock::Handler->new( $r, cache => cache() );

  my $s3_config = eval { return get_s3_config( $bedrock_handler->config->{s3} ); };

  return set_error_status( $r, $EVAL_ERROR )
    if !$s3_config || $EVAL_ERROR;

  if ( $s3_config->get_s3 && $s3_config->get_bucket ) {
    $r->log->debug( sprintf 'S3 session files configured for BUCKET [%s]', $s3_config->get_bucket_name );
  }

  # NOTE: get_session_file_info() validates that we have a login
  # session AND normally can throw an exception if the file does not
  # exist. We optionally allow this module to serve both local session
  # files and S3 files.
  my $allow_local_session_files = to_boolean( $s3_config->{local_session_files} );

  my $filename = eval {
    my $file_info = get_session_file_info( $bedrock_handler, { verify => $FALSE, verify_user => $FALSE } );
    return $file_info->{filename};
  };

  return set_error_status( $r, $EVAL_ERROR )
    if !$filename || $EVAL_ERROR;

  $r->log->debug( sprintf 'attempting to serve file...[%s]', $filename );

  # TBD: use Apache to service file...requires hooking this is at
  # PerlFixupHandler phase...
  #
  # <IfModule mod_perl.c>
  #   SetHandler perl-script
  #   PerlFixupHandler Apache::BedrockCloudSessionFiles
  # </IfModule>

  return send_file( $r, $filename )
    if $allow_local_session_files && -e $filename;

  my $status = eval { return send_s3_file( $r, $filename ); };

  return $status && !$EVAL_ERROR ? $OK : set_error_status( $r, $EVAL_ERROR );
}

1;

## no critic

__END__

=pod

=head1 NAME

Apache::BedrockCloudSessionFiles - serve files from local file system or S3

=head1 SYNOPSIS

  <Directory /var/www/vhosts/mysite/session>
    AcceptPathInfo On
    Options -Indexes
  
    <IfModule mod_perl.c>
      SetHandler perl-script
      PerlHandler Apache::BedrockCloudSessionFiles
      PerlSetEnv AWS_BUCKET mybucket
      PerlSetEmv S3_HOST localstack_main:4566
    </IfModule>
  
    <IfModule !mod_perl.c>
      SetHandler bedrock-session-files
    </IfModule>
  
  </Directory>

=head1 DESCRIPTION

Implements an Apache handler that serves files from a local session
directory or an S3 bucket.  This is typically used when a
web application wishes to serve a private file to a user, or make a
file available for only a short period of time to a specific user
session. A typical URI for this type of asset might look like:

 /session/foo.pdf

In other words, the asset would be protected since the same URL would
not access the asset for anyone other than the requestor since it is
specific to their session. Sessions are identified using a session cookie.

=head1 NOTES

Use in conjunction with L<BLM::Startup::S3> for best results ;-)

Example:

Read a PDF from S3 and write to the current user's cloud session
directory.

 <!-- read a file from somewher -->
 <null:pdf $s3.get_key('/private/private.pdf')>

 <!-- write file to user's cloud session directory -->
 <null:session_file '%s/private.pdf'>

 <null $s3.add_key($session_file.sprintf($session.session), $pdf>

 <!-- redirect the to the session file -->
 <null $header.see_other('/session/private.pdf')>

=head2 Setting Up the Apache Handler

Setup the handler in your Apache configuration file as shown below:

  Action bedrock-cloudsession-files /cgi-bin/bedrock-session-files.cgi virtual

  Alias /session /var/www/vhosts/mysite/session

  <Directory /var/www/vhosts/mysite/session>
    AcceptPathInfo On
    Options -Indexes
  
    <IfModule mod_perl.c>
      SetHandler perl-script
      PerlHandler Apache::BedrockCloudSessionFiles
      PerlSetEnv AWS_BUCKET mybucket
      PerlSetEmv S3_HOST localstack_main:4566
    </IfModule>
  
    <IfModule !mod_perl.c>
      SetHandler bedrock-session-files
    </IfModule>
  
  </Directory>

If you want to use the CGI version instead of the C<mod_perl> version
of the handler, copy the CGI handler to your F</cgi-bin>
directory. F<bedrock-session-files.cgi> is distributed as part of
Bedrock and can be found at
F</usr/local/lib/bedrock/cgi-bin/bedrock-session-files.cgi>.

=head1 SEE OTHER

L<Bedrock::Handler>, L<Bedrock::Apache::Request_cgi>, L<Amazon::S3>

=head1 AUTHOR

Rob Lauer - <bigfoot@cpan.org>

=cut
